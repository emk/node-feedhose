#!/usr/bin/env coffee

# Require the modules we need.
sys = require 'sys'
express = require 'express'
socket_io = require './vendor/Socket.IO-node'
feedhose = require './feedhose'
utilities = require './utilities'

# Kick-start our feedhose client.
feed = new feedhose.Client 'http://hose.scripting.com/'
feed.on 'item', (item) ->
  console.log item['title']
  console.log item['link']

# Set up a web server.
app = express.createServer()
app.get "/stats", (req, res) ->
  console.log "/stats"
  stats =
    clients: feed.listeners('item').length - 1
    lastResponse: utilities.iso8601(feed.last_response)
  res.contentType 'application/json'
  res.send JSON.stringify(stats)
app.use express.staticProvider(__dirname + '/public')
app.listen 8000

# Hook up a socket.io server.
socket_server = socket_io.listen app
socket_server.on 'connection', (client) ->
  # Prime the client with some recent items.
  for item in feed.recent_items
    client.send item

  # Route new items to the client as they arrive.
  route_to_client = (item) -> client.send item
  feed.on 'item', route_to_client
  client.on 'message', (data) ->
    # Ignore inbound messages.
  client.on 'disconnect', () ->
    feed.removeListener 'item', route_to_client
  client.on 'error', () ->
    feed.removeListener 'item', route_to_client
