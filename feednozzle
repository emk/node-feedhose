#!/usr/bin/env coffee

# Require the modules we need.
sys = require 'sys'
express = require 'express'
socket_io = require './vendor/Socket.IO-node'
feedhose = require './feedhose'
utilities = require './utilities'

# Kick-start our feedhose client.
feed = new feedhose.Client 'http://hose.scripting.com/'
feed.on 'item', (item) ->
  console.log item['title']
  console.log item['link']

# Set up a web server.
app = express.createServer()
app.configure ->
  app.use app.router
  app.use express.staticProvider(__dirname + '/public')

# Query this URL to get our current server stats.
app.get "/stats", (req, res) ->
  stats =
    time: utilities.iso8601(new Date())
    clients: feed.listeners('item').length - 1
    lastResponse: utilities.iso8601(feed.last_response)
  res.contentType 'application/json'
  res.send JSON.stringify(stats)

# You can query this URL with monit to see if the server is live.
app.get "/live", (req, res) ->
  if new Date() - feed.last_response > 15*60*1000
    res.send "Connection lost?\n", 500
  else
    res.send "Looks good.\n", 200

# Serve on port 8000 by default.
app.listen(parseInt(process.env['FEEDNOZZLE_PORT'] ? '8000'))

# Hook up a socket.io server.
socket_server = socket_io.listen app
socket_server.on 'connection', (client) ->
  send_item_to_client = (item) -> client.send item: item

  # Prime the client with some recent items.
  for item in feed.recent_items
    send_item_to_client item

  # Route new items to the client as they arrive.
  feed.on 'item', send_item_to_client
  client.on 'message', (data) ->
    # Ignore inbound messages.
  client.on 'disconnect', () ->
    feed.removeListener 'item', send_item_to_client
  client.on 'error', () ->
    feed.removeListener 'item', send_item_to_client

# Broadcast a heartbeat to our clients.  We don't trust Socket.io to
# keep pages alive.
broadcast_heartbeat = ->
  socket_server.broadcast command: 'heartbeat'
setInterval broadcast_heartbeat, 2*60*1000
